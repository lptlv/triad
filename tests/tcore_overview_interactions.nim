import tcore_support

proc hasOverviewBroadcast(effects: seq[Effect], open: bool): bool =
  effects.anyIt(
    it.kind == EffectKind.EffBroadcastTriadJson and it.triadEventName == "state" and
      parseJson(it.jsonPayload)["triad"]["state"]["overview"]["is_open"].getBool() ==
        open
  )

proc twoOutputOverviewModel(): Model =
  result = configuredModel()
  result.applyMsg(
    Msg(kind: MsgKind.OutputDimensions, outputId: 1, width: 1000, height: 700)
  )
  result.applyMsg(Msg(kind: MsgKind.OutputName, nameOutputId: 1, outputName: "DP-1"))
  result.applyMsg(
    Msg(kind: MsgKind.OutputDimensions, outputId: 2, width: 900, height: 700)
  )
  result.applyMsg(
    Msg(kind: MsgKind.OutputPosition, positionOutputId: 2, outputX: 1000, outputY: 0)
  )
  result.applyMsg(Msg(kind: MsgKind.OutputName, nameOutputId: 2, outputName: "DP-2"))
  result.applyMsg(
    Msg(kind: MsgKind.WindowCreated, windowId: 1, appId: "app", title: "One")
  )
  result.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 2))
  result.applyMsg(Msg(kind: MsgKind.CmdMoveWorkspaceToOutput, outputTarget: "DP-2"))
  result.applyMsg(
    Msg(kind: MsgKind.WindowCreated, windowId: 2, appId: "app", title: "Two")
  )
  result.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 1))
  result.applyMsg(Msg(kind: MsgKind.CmdOpenOverview))

proc rectIntersection(a, b: Rect): Rect =
  let x1 = max(a.x, b.x)
  let y1 = max(a.y, b.y)
  let x2 = min(a.x + a.w, b.x + b.w)
  let y2 = min(a.y + a.h, b.y + b.h)
  if x2 <= x1 or y2 <= y1:
    return Rect(x: x1, y: y1, w: 0, h: 0)
  Rect(x: x1, y: y1, w: x2 - x1, h: y2 - y1)

proc positiveArea(rect: Rect): bool =
  rect.w > 0 and rect.h > 0

proc projectedInstruction(model: Model, id: uint32): RenderInstruction =
  for instr in model.layoutProjection().instructions:
    if uint32(instr.windowId) == id:
      return instr
  RenderInstruction()

suite "Core Runtime Logic: overview interactions":
  test "Dragging unified overview preview moves window without closing":
    var model = configuredModel()
    model.applyMsg(
      Msg(kind: MsgKind.OutputDimensions, outputId: 0, width: 1000, height: 700)
    )
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 1, appId: "app", title: "One")
    )
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 2))
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 2, appId: "app", title: "Two")
    )
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 1))
    model.applyMsg(Msg(kind: MsgKind.CmdOpenOverview))

    let start = model.instructionGeom(1).rectCenter()
    let slots = model.previewSlots()
    let target =
      model.workspacePreviewRect(model.primaryScreen(), slots, 1).rectCenter()
    discard model.updateModel(
      Msg(
        kind: MsgKind.OverviewPointerDragRequested,
        overviewDragWinId: 1,
        overviewDragX: start.x,
        overviewDragY: start.y,
      )
    )
    model.applyMsg(
      Msg(kind: MsgKind.PointerDelta, dx: target.x - start.x, dy: target.y - start.y)
    )
    model.applyMsg(Msg(kind: MsgKind.PointerRelease))

    check model.overviewActive
    check model.activeTag == model.tagForSlot(1)
    check model.activeWorkspaceFocusId() == 0
    check model.firstWindowPosition(WindowId(1)).tagId == model.tagForSlot(2)

  test "Right-dragging unified overview pans hovered workspace camera":
    var model = configuredModel()
    model.applyMsg(
      Msg(kind: MsgKind.OutputDimensions, outputId: 0, width: 1000, height: 700)
    )
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 1, appId: "app", title: "One")
    )
    model.applyMsg(Msg(kind: MsgKind.CmdOpenOverview))
    let beforeViewport = model.viewport(1)

    let start = model.instructionGeom(1).rectCenter()
    discard model.updateModel(
      Msg(
        kind: MsgKind.OverviewPointerScrollRequested,
        overviewScrollX: start.x,
        overviewScrollY: start.y,
      )
    )
    model.applyMsg(Msg(kind: MsgKind.PointerDelta, dx: 50, dy: 0))
    model.applyMsg(Msg(kind: MsgKind.PointerRelease))

    check model.overviewActive
    check model.viewport(1).currentViewportXOffset ==
      beforeViewport.currentViewportXOffset - 100.0'f32
    check model.viewport(1).targetViewportXOffset ==
      beforeViewport.targetViewportXOffset - 100.0'f32
    check model.pointerOp.kind == PointerOpKind.OpNone

  test "Wheel over unified overview switches workspaces vertically":
    var model = configuredModel()
    model.applyMsg(
      Msg(kind: MsgKind.OutputDimensions, outputId: 0, width: 1000, height: 700)
    )
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 1, appId: "app", title: "One")
    )
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 2))
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 2, appId: "app", title: "Two")
    )
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 1))
    model.applyMsg(Msg(kind: MsgKind.CmdOpenOverview))

    let slots = model.previewSlots()
    let target = model
      .workspacePreviewRect(model.primaryScreen(), slots, slots.find(1'u32))
      .rectCenter()
    let effects = model.updateModel(
      Msg(
        kind: MsgKind.OverviewWheel,
        overviewWheelX: target.x,
        overviewWheelY: target.y,
        overviewWheelHorizontal: 0,
        overviewWheelVertical: 1,
      )
    )

    check model.overviewActive
    check model.activeTag == model.tagForSlot(2)
    check model.selectedOverviewWindow() == WindowId(2)
    check effects.anyIt(it.kind == EffectKind.EffFocusShellUi)
    check effects.anyIt(
      it.kind == EffectKind.EffBroadcastTriadJson and it.triadEventName == "layout"
    )

  test "Wheel over unified overview focuses columns horizontally":
    var model = configuredModel()
    model.applyMsg(
      Msg(kind: MsgKind.OutputDimensions, outputId: 0, width: 1000, height: 700)
    )
    model.applyMsg(Msg(kind: MsgKind.CmdSetLayout, newLayout: LayoutMode.Scroller))
    for id in 1'u32 .. 3'u32:
      model.applyMsg(
        Msg(
          kind: MsgKind.WindowCreated,
          windowId: id,
          appId: "app",
          title: "Window " & $id,
        )
      )
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWindowById, focusWindowId: 1))
    model.applyMsg(Msg(kind: MsgKind.CmdOpenOverview))

    let slots = model.previewSlots()
    let target = model
      .workspacePreviewRect(model.primaryScreen(), slots, slots.find(1'u32))
      .rectCenter()
    let horizontalEffects = model.updateModel(
      Msg(
        kind: MsgKind.OverviewWheel,
        overviewWheelX: target.x,
        overviewWheelY: target.y,
        overviewWheelHorizontal: 1,
        overviewWheelVertical: 0,
      )
    )

    check model.activeTag == model.tagForSlot(1)
    check model.selectedOverviewWindow() == WindowId(2)
    check not horizontalEffects.anyIt(
      it.kind == EffectKind.EffBroadcastTriadJson and it.triadEventName == "layout"
    )

    model.applyMsg(
      Msg(kind: MsgKind.ModifiersChanged, oldModifiers: 0'u32, newModifiers: 1'u32)
    )
    let shiftEffects = model.updateModel(
      Msg(
        kind: MsgKind.OverviewWheel,
        overviewWheelX: target.x,
        overviewWheelY: target.y,
        overviewWheelHorizontal: 0,
        overviewWheelVertical: 1,
      )
    )

    check model.activeTag == model.tagForSlot(1)
    check model.selectedOverviewWindow() == WindowId(3)
    check not shiftEffects.anyIt(
      it.kind == EffectKind.EffBroadcastTriadJson and it.triadEventName == "layout"
    )

  test "Holding unified overview drag waits for release before moving window":
    var model = configuredModel()
    model.applyMsg(
      Msg(kind: MsgKind.OutputDimensions, outputId: 0, width: 1000, height: 700)
    )
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 1, appId: "app", title: "One")
    )
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 2))
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 2, appId: "app", title: "Two")
    )
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 1))
    model.applyMsg(Msg(kind: MsgKind.CmdOpenOverview))

    let start = model.instructionGeom(1).rectCenter()
    let slots = model.previewSlots()
    let target =
      model.workspacePreviewRect(model.primaryScreen(), slots, 1).rectCenter()
    model.applyMsg(
      Msg(
        kind: MsgKind.OverviewPointerDragRequested,
        overviewDragWinId: 1,
        overviewDragX: start.x,
        overviewDragY: start.y,
      )
    )
    model.applyMsg(
      Msg(kind: MsgKind.PointerDelta, dx: target.x - start.x, dy: target.y - start.y)
    )
    for _ in 0 ..< 47:
      model.applyMsg(Msg(kind: MsgKind.CmdTick))

    check model.overviewActive
    check model.activeTag == model.tagForSlot(1)
    check model.firstWindowPosition(WindowId(1)).tagId == model.tagForSlot(1)
    check model.pointerOp.kind == PointerOpKind.OpOverviewDrag

    model.applyMsg(Msg(kind: MsgKind.PointerRelease))

    check model.overviewActive
    check model.activeTag == model.tagForSlot(1)
    check model.firstWindowPosition(WindowId(1)).tagId == model.tagForSlot(2)
    check model.pointerOp.kind == PointerOpKind.OpNone

  test "Blank click on secondary output activates that output workspace":
    var model = twoOutputOverviewModel()
    let second = model.outputForExternal(ExternalOutputId(2))
    let slots = model.previewSlotsForOutput(second)
    let idx = slots.find(2'u32)
    let target = model.workspacePreviewRectForOutput(
      model.outputScreen(second), slots, idx, second
    )

    model.applyMsg(
      Msg(
        kind: MsgKind.OverviewPointerDragRequested,
        overviewDragWinId: 0,
        overviewDragX: target.x + 1,
        overviewDragY: target.y + 1,
      )
    )
    model.applyMsg(Msg(kind: MsgKind.PointerRelease))

    check not model.overviewActive
    check model.activeOutput == second
    check model.activeTag == model.tagForSlot(2)
    check model.focusedWindowId() == 2

  test "Window click on secondary output focuses that window":
    var model = twoOutputOverviewModel()
    let second = model.outputForExternal(ExternalOutputId(2))
    let start = model.instructionGeom(2).rectCenter()

    model.applyMsg(
      Msg(
        kind: MsgKind.OverviewPointerDragRequested,
        overviewDragWinId: 2,
        overviewDragX: start.x,
        overviewDragY: start.y,
      )
    )
    model.applyMsg(Msg(kind: MsgKind.PointerRelease))

    check not model.overviewActive
    check model.activeOutput == second
    check model.focusedWindowId() == 2

  test "Dragging overview window on secondary output releases pointer op":
    var model = twoOutputOverviewModel()
    let start = model.instructionGeom(2).rectCenter()
    let target = (x: start.x + 32'i32, y: start.y)

    model.applyMsg(
      Msg(
        kind: MsgKind.OverviewPointerDragRequested,
        overviewDragWinId: 2,
        overviewDragX: start.x,
        overviewDragY: start.y,
      )
    )
    model.applyMsg(
      Msg(kind: MsgKind.PointerDelta, dx: target.x - start.x, dy: target.y - start.y)
    )
    model.applyMsg(Msg(kind: MsgKind.PointerRelease))

    check model.overviewActive
    check model.pointerOp.kind == PointerOpKind.OpNone
    check model.firstWindowPosition(WindowId(2)).tagId == model.tagForSlot(2)

  test "Dragging overview window across outputs moves to target workspace":
    var model = twoOutputOverviewModel()
    let second = model.outputForExternal(ExternalOutputId(2))
    let start = model.instructionGeom(1).rectCenter()
    let slots = model.previewSlotsForOutput(second)
    let target = model
      .workspacePreviewRectForOutput(
        model.outputScreen(second), slots, slots.find(2'u32), second
      )
      .rectCenter()

    model.applyMsg(
      Msg(
        kind: MsgKind.OverviewPointerDragRequested,
        overviewDragWinId: 1,
        overviewDragX: start.x,
        overviewDragY: start.y,
      )
    )
    model.applyMsg(
      Msg(kind: MsgKind.PointerDelta, dx: target.x - start.x, dy: target.y - start.y)
    )
    model.applyMsg(Msg(kind: MsgKind.PointerRelease))

    check model.overviewActive
    check model.activeTag == model.tagForSlot(1)
    check model.firstWindowPosition(WindowId(1)).tagId == model.tagForSlot(2)
    check model.pointerOp.kind == PointerOpKind.OpNone

  test "Dragging overview window renders across both outputs while held":
    var model = twoOutputOverviewModel()
    let first = model.outputForExternal(ExternalOutputId(1))
    let second = model.outputForExternal(ExternalOutputId(2))
    let firstScreen = model.outputScreen(first)
    let secondScreen = model.outputScreen(second)
    let start = model.instructionGeom(1).rectCenter()

    model.applyMsg(
      Msg(
        kind: MsgKind.OverviewPointerDragRequested,
        overviewDragWinId: 1,
        overviewDragX: start.x,
        overviewDragY: start.y,
      )
    )

    let base = model.projectedInstruction(1).geom
    let boundary = firstScreen.x + firstScreen.w
    model.applyMsg(
      Msg(kind: MsgKind.PointerDelta, dx: boundary - base.rectCenter().x, dy: 0)
    )

    let dragged = model.projectedInstruction(1)
    let fullBounds = model.overviewDragVisibilityBounds()
    let fullVisibility = renderVisibility(dragged.geom, fullBounds, 4)
    let activeVisibility =
      renderVisibility(dragged.geom, model.activeWorkspaceScreen(), 4)

    check model.pointerOp.kind == PointerOpKind.OpOverviewDrag
    check uint32(dragged.windowId) == 1
    check not dragged.clipSet
    check dragged.geom.rectIntersection(firstScreen).positiveArea()
    check dragged.geom.rectIntersection(secondScreen).positiveArea()
    check fullVisibility.visible
    check not fullVisibility.clipped
    check activeVisibility.visible
    check activeVisibility.clipped

  test "Clicking overview window commits focus":
    var model = configuredModel()
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 1, appId: "app", title: "One")
    )
    model.applyMsg(Msg(kind: MsgKind.CmdFocusTag, focusTag: 2))
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 2, appId: "app", title: "Two")
    )
    model.applyMsg(Msg(kind: MsgKind.CmdFocusTag, focusTag: 1))
    model.applyMsg(Msg(kind: MsgKind.CmdOpenOverview))

    let effects = model.updateModel(Msg(kind: MsgKind.FocusChanged, newFocusedId: 2))

    check not model.overviewActive
    check model.activeTag == model.tagForSlot(2)
    check model.focusedWindowId() == 2
    check effects.hasOverviewBroadcast(false)
    check effects.anyIt(
      it.kind == EffectKind.EffFocusWindow and uint32(it.focusId) == 2
    )

  test "Clicking blank unified overview workspace activates workspace":
    var model = configuredModel()
    model.applyMsg(
      Msg(kind: MsgKind.OutputDimensions, outputId: 0, width: 1000, height: 700)
    )
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 1, appId: "app", title: "One")
    )
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 2))
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 2, appId: "app", title: "Two")
    )
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 1))
    model.applyMsg(Msg(kind: MsgKind.CmdOpenOverview))

    let slots = model.previewSlots()
    let target = model.workspacePreviewRect(model.primaryScreen(), slots, 1)
    model.applyMsg(
      Msg(
        kind: MsgKind.OverviewPointerDragRequested,
        overviewDragWinId: 0,
        overviewDragX: target.x + 1,
        overviewDragY: target.y + 1,
      )
    )
    let effects = model.updateModel(Msg(kind: MsgKind.PointerRelease))

    check not model.overviewActive
    check model.activeTag == model.tagForSlot(2)
    check model.focusedWindowId() == 2

  test "Overview hides trailing dynamic empty workspace":
    var model = configuredModel()
    model.applyMsg(
      Msg(kind: MsgKind.OutputDimensions, outputId: 0, width: 1000, height: 700)
    )
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 1, appId: "app", title: "One")
    )
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 3))
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 3, appId: "app", title: "Three")
    )
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 1))
    model.applyMsg(Msg(kind: MsgKind.CmdOpenOverview))

    let slots = model.previewSlots()
    check slots == @[1'u32, 3'u32]
    check slots.find(4'u32) == -1
    check model.overviewActive
    check model.activeTag == model.tagForSlot(1)

  test "Overview select retargets same-workspace camera":
    var model = cameraModel()
    model.seedCameraWindows()
    model.setViewport(1, targetX = 125.0, currentX = 125.0)

    let beforeViewport = model.viewport(1)

    model.applyMsg(Msg(kind: MsgKind.CmdOpenOverview))
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWindowById, focusWindowId: 1))
    model.applyMsg(Msg(kind: MsgKind.CmdSelectWindow))

    check model.focusedWindowId() == 1
    check model.viewport(1) == beforeViewport
    discard model.layoutInstructions()
    check model.viewport(1).currentViewportXOffset ==
      beforeViewport.currentViewportXOffset
    check model.viewport(1).targetViewportXOffset != beforeViewport.targetViewportXOffset

  test "Unified overview camera retarget animates while overview is open":
    var model = cameraModel()
    model.seedCameraWindows()
    model.setViewport(1, targetX = 0.0, currentX = 0.0)

    model.applyMsg(Msg(kind: MsgKind.CmdOpenOverview))
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWindowById, focusWindowId: 1))
    discard model.layoutInstructions()
    let target = model.viewport(1).targetViewportXOffset

    check model.overviewActive
    check target != 0.0'f32
    check model.viewport(1).currentViewportXOffset == 0.0'f32

    discard model.updateModel(Msg(kind: MsgKind.CmdTick))

    check model.viewport(1).currentViewportXOffset != 0.0'f32
    check model.viewport(1).currentViewportXOffset != target
    let afterTick = model.viewport(1)

    model.applyMsg(Msg(kind: MsgKind.CmdCloseOverview))

    check not model.overviewActive
    check model.viewport(1) == afterTick

  test "Unified overview ticks non-active preview workspace cameras":
    var model = cameraModel()
    model.seedCameraWindows(1)
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 2))
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 2, appId: "app", title: "Two")
    )
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 3, appId: "app", title: "Three")
    )
    model.setViewport(2, targetX = 0.0, currentX = 0.0)
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 1))

    model.applyMsg(Msg(kind: MsgKind.CmdOpenOverview))
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWindowById, focusWindowId: 2))
    discard model.layoutInstructions()
    let target = model.viewport(2).targetViewportXOffset
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 1))

    check model.overviewActive
    check model.activeTag == model.tagForSlot(1)
    check target != 0.0'f32
    check model.viewport(2).currentViewportXOffset == 0.0'f32

    discard model.updateModel(Msg(kind: MsgKind.CmdTick))

    check model.viewport(2).currentViewportXOffset != 0.0'f32
    check model.viewport(2).currentViewportXOffset != target

  test "Overview select retargets target workspace camera":
    var model = cameraModel()
    model.seedCameraWindows(1)
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 2))
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 2, appId: "app", title: "Two")
    )
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 3, appId: "app", title: "Three")
    )
    model.setViewport(2, targetX = 250.0, currentX = 175.0)
    let workspace2Viewport = model.viewport(2)

    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 1))
    model.setViewport(1, targetX = 80.0, currentX = 80.0)
    let workspace1Viewport = model.viewport(1)

    model.applyMsg(Msg(kind: MsgKind.CmdOpenOverview))
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWindowById, focusWindowId: 2))
    model.applyMsg(Msg(kind: MsgKind.CmdSelectWindow))

    check model.activeTag == model.tagForSlot(2)
    check model.focusedWindowId() == 2
    check model.viewport(1) == workspace1Viewport
    check model.viewport(2) == workspace2Viewport
    discard model.layoutInstructions()
    check model.viewport(1) == workspace1Viewport
    check model.viewport(2).currentViewportXOffset ==
      workspace2Viewport.currentViewportXOffset
    check model.viewport(2).targetViewportXOffset !=
      workspace2Viewport.targetViewportXOffset

  test "Closing unified overview preserves camera changes":
    var model = cameraModel()
    model.seedCameraWindows()
    model.applyMsg(Msg(kind: MsgKind.CmdSetLayout, newLayout: LayoutMode.Grid))
    model.setViewport(1, targetX = 300.0, currentX = 100.0)
    let beforeViewport = model.viewport(1)

    model.applyMsg(Msg(kind: MsgKind.CmdOpenOverview))
    discard model.updateModel(Msg(kind: MsgKind.CmdTick))
    let afterTick = model.viewport(1)
    model.applyMsg(Msg(kind: MsgKind.CmdCloseOverview))

    check model.viewport(1) == afterTick
    check model.viewport(1) != beforeViewport

  test "Workspace round trip preserves each camera":
    var model = cameraModel()
    model.seedCameraWindows(1)
    model.setViewport(1, targetX = 300.0, currentX = 0.0)
    let workspace1Viewport = model.viewport(1)

    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 2))
    model.applyMsg(
      Msg(kind: MsgKind.WindowCreated, windowId: 2, appId: "app", title: "Two")
    )
    model.setViewport(2, targetX = 75.0, currentX = 75.0)
    let workspace2Viewport = model.viewport(2)

    for _ in 0 ..< 4:
      discard model.updateModel(Msg(kind: MsgKind.CmdTick))
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWorkspaceIndex, workspaceIndex: 1))

    check model.viewport(1) == workspace1Viewport
    check model.viewport(2) == workspace2Viewport

  test "Normal focus navigation can retarget camera":
    var model = cameraModel()
    model.seedCameraWindows()
    model.setViewport(1, targetX = 0.0, currentX = 0.0)

    model.applyMsg(Msg(kind: MsgKind.CmdFocusWindowById, focusWindowId: 1))
    discard model.layoutInstructions()

    check model.viewport(1).targetViewportXOffset != 0.0'f32

  test "External focus observation uses normal focus path":
    var model = cameraModel()
    model.seedCameraWindows()
    model.applyMsg(Msg(kind: MsgKind.CmdFocusWindowById, focusWindowId: 2))
    discard model.layoutInstructions()
    model.setViewport(1, targetX = 0.0, currentX = 0.0)

    let effects = model.updateModel(Msg(kind: MsgKind.FocusChanged, newFocusedId: 1))
    discard model.layoutInstructions()

    check model.focusedWindowId() == 1
    check effects.anyIt(
      it.kind == EffectKind.EffFocusWindow and uint32(it.focusId) == 1
    )
    check effects.anyIt(it.kind == EffectKind.EffManageDirty)
    check model.viewport(1).targetViewportXOffset != 0.0'f32
