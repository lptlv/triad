import std/[algorithm, json, options, tables]
import layout_descriptor_codec
import layout_mode_codec
import layout_selection_codec
import native_layout_codec
import shell_focus
import ../types/shell_snapshot
from ../types/runtime_values import
  Direction, FrameNodeKind, FrameSplitOrientation, LayoutMode, LayoutSelectionKind,
  LayoutSource, SplitTreeNodeMode, WindowRuleIdleInhibitMode

export shell_snapshot

proc nullableString(value: string): JsonNode =
  if value.len == 0:
    newJNull()
  else:
    %value

proc triadSupportedLayoutsJson*(snapshot: ShellSnapshot): JsonNode =
  result = newJArray()
  for mode in LayoutMode:
    if mode.layoutSource() == LayoutSource.Core:
      result.add(
        %*{
          "kind": "builtin",
          "id": layoutModeId(mode),
          "ordinal": ord(mode),
          "runtime_kind": mode.layoutKind().layoutKindId(),
          "layout_source": mode.layoutSource().layoutSourceId(),
        }
      )
  for layout in snapshot.customLayouts:
    let id = layout.id.layoutIdString()
    result.add(
      %*{
        "kind": "custom",
        "id": id,
        "fallback_layout": layout.fallback.selectionFallbackId(),
        "runtime_kind": id.layoutKindForId().layoutKindId(),
        "layout_source": id.layoutSourceForId().layoutSourceId(),
      }
    )
  for layout in snapshot.nativeLayouts:
    let id = layout.id.nativeLayoutIdString()
    result.add(
      %*{
        "kind": "native",
        "id": id,
        "fallback_layout": layout.fallback.selectionFallbackId(),
        "runtime_kind": id.layoutKindForId().layoutKindId(),
        "layout_source": id.layoutSourceForId().layoutSourceId(),
      }
    )

proc triadLayoutCycleJson*(snapshot: ShellSnapshot): JsonNode =
  result = newJArray()
  if snapshot.layoutCycleSelections.len > 0:
    for selection in snapshot.layoutCycleSelections:
      result.add(%selection.selectionId())
  else:
    for mode in snapshot.layoutCycle:
      result.add(%layoutModeId(mode))

proc triadLayoutCycleEntriesJson*(snapshot: ShellSnapshot): JsonNode =
  result = newJArray()
  for selection in snapshot.layoutCycleSelections:
    case selection.kind
    of LayoutSelectionKind.Builtin:
      result.add(%*{"kind": "builtin", "id": layoutModeId(selection.builtin)})
    of LayoutSelectionKind.Custom:
      result.add(
        %*{
          "kind": "custom",
          "id": selection.customId.layoutIdString(),
          "fallback_layout": selection.selectionFallbackId(),
        }
      )
    of LayoutSelectionKind.Native:
      result.add(
        %*{
          "kind": "native",
          "id": selection.nativeId.nativeLayoutIdString(),
          "fallback_layout": layoutModeId(selection.builtin),
        }
      )

proc triadColumnJson(col: ShellColumn): JsonNode =
  let windows = newJArray()
  for winId in col.windows:
    windows.add(%winId)
  %*{
    "idx": col.idx,
    "width_proportion": col.widthProportion,
    "scroller_single_proportion": col.scrollerSingleProportion,
    "is_full_width": col.isFullWidth,
    "windows": windows,
  }

proc frameNodeKindId(kind: FrameNodeKind): string =
  case kind
  of FrameNodeKind.Leaf: "leaf"
  of FrameNodeKind.Split: "split"

proc frameSplitOrientationId(orientation: FrameSplitOrientation): string =
  case orientation
  of FrameSplitOrientation.Horizontal: "horizontal"
  of FrameSplitOrientation.Vertical: "vertical"

proc directionId(direction: Direction): string =
  case direction
  of Direction.DirLeft: "left"
  of Direction.DirRight: "right"
  of Direction.DirUp: "up"
  of Direction.DirDown: "down"

proc splitTreeNodeModeId(mode: SplitTreeNodeMode): string =
  case mode
  of SplitTreeNodeMode.SplitH: "split-h"
  of SplitTreeNodeMode.SplitV: "split-v"
  of SplitTreeNodeMode.Stacking: "stacking"
  of SplitTreeNodeMode.Tabbed: "tabbed"

proc triadFrameJson(frame: ShellFrame): JsonNode =
  let windows = newJArray()
  for winId in frame.windows:
    windows.add(%winId)
  %*{
    "id": frame.id,
    "kind": frame.kind.frameNodeKindId(),
    "parent":
      if frame.parent == 0:
        newJNull()
      else:
        %frame.parent,
    "first_child":
      if frame.firstChild == 0:
        newJNull()
      else:
        %frame.firstChild,
    "second_child":
      if frame.secondChild == 0:
        newJNull()
      else:
        %frame.secondChild,
    "orientation": frame.orientation.frameSplitOrientationId(),
    "ratio": frame.ratio,
    "windows": windows,
    "active_window_id":
      if frame.activeWindow == 0:
        newJNull()
      else:
        %frame.activeWindow,
    "focused": frame.focused,
  }

proc triadBspNodeJson(node: ShellBspNode): JsonNode =
  %*{
    "id": node.id,
    "kind": node.kind.frameNodeKindId(),
    "parent":
      if node.parent == 0:
        newJNull()
      else:
        %node.parent,
    "first_child":
      if node.firstChild == 0:
        newJNull()
      else:
        %node.firstChild,
    "second_child":
      if node.secondChild == 0:
        newJNull()
      else:
        %node.secondChild,
    "orientation": node.orientation.frameSplitOrientationId(),
    "ratio": node.ratio,
    "window_id":
      if node.window == 0:
        newJNull()
      else:
        %node.window,
    "focused": node.focused,
    "preselect_direction":
      if node.hasPreselection:
        %node.preselectDirection.directionId()
      else:
        newJNull(),
    "preselect_ratio":
      if node.hasPreselection:
        %node.preselectRatio
      else:
        newJNull(),
  }

proc triadSplitNodeJson(node: ShellSplitNode): JsonNode =
  let children = newJArray()
  for child in node.children:
    children.add(%child)
  %*{
    "id": node.id,
    "kind": node.kind.frameNodeKindId(),
    "parent":
      if node.parent == 0:
        newJNull()
      else:
        %node.parent,
    "children": children,
    "mode": node.mode.splitTreeNodeModeId(),
    "last_split_mode": node.lastSplitMode.splitTreeNodeModeId(),
    "weight": node.weight,
    "window_id":
      if node.window == 0:
        newJNull()
      else:
        %node.window,
    "focused": node.focused,
  }

proc triadWorkspaceLayoutJson*(workspace: ShellWorkspace): JsonNode =
  let columns = newJArray()
  for col in workspace.columns:
    columns.add(triadColumnJson(col))
  let frames = newJArray()
  for frame in workspace.frames:
    frames.add(triadFrameJson(frame))
  let bspNodes = newJArray()
  for node in workspace.bspNodes:
    bspNodes.add(triadBspNodeJson(node))
  let splitNodes = newJArray()
  for node in workspace.splitNodes:
    splitNodes.add(triadSplitNodeJson(node))

  %*{
    "tag_id": workspace.tagId,
    "workspace_idx": workspace.workspaceIdx,
    "name": nullableString(workspace.name),
    "output": nullableString(workspace.outputName),
    "layout": workspace.layoutId,
    "layout_kind": workspace.layoutKind,
    "runtime_kind": workspace.runtimeLayoutKind,
    "layout_source": workspace.layoutSource,
    "fallback_layout": workspace.fallbackLayout,
    "is_configured": workspace.isConfigured,
    "is_active": workspace.isActive,
    "is_output_visible": workspace.isOutputVisible,
    "is_urgent": false,
    "occupied": workspace.occupied,
    "focused_window_id":
      if workspace.focusedWindow == 0:
        newJNull()
      else:
        %workspace.focusedWindow,
    "columns": columns,
    "frames": frames,
    "bsp_nodes": bspNodes,
    "split_nodes": splitNodes,
    "master_count": workspace.masterCount,
    "master_split_ratio": workspace.masterSplitRatio,
    "viewport": {
      "target_x": workspace.targetViewportXOffset,
      "current_x": workspace.currentViewportXOffset,
      "target_y": workspace.targetViewportYOffset,
      "current_y": workspace.currentViewportYOffset,
    },
  }

proc triadWorkspacesJson*(snapshot: ShellSnapshot): JsonNode =
  let workspaces = newJArray()
  for workspace in snapshot.workspaces:
    workspaces.add(triadWorkspaceLayoutJson(workspace))
  result = workspaces

proc triadLayoutStateJson*(snapshot: ShellSnapshot): JsonNode =
  %*{
    "version": snapshot.version,
    "layouts": triadSupportedLayoutsJson(snapshot),
    "layout_cycle": triadLayoutCycleJson(snapshot),
    "layout_cycle_entries": triadLayoutCycleEntriesJson(snapshot),
    "active_tag": snapshot.activeTag,
    "active_workspace_idx": snapshot.activeWorkspaceIdx,
    "workspaces": triadWorkspacesJson(snapshot),
  }

proc outputTransformId(transform: int32): string =
  case transform
  of 1: "90"
  of 2: "180"
  of 3: "270"
  of 4: "Flipped"
  of 5: "Flipped90"
  of 6: "Flipped180"
  of 7: "Flipped270"
  else: "Normal"

proc triadOutputJson(output: ShellOutput): JsonNode =
  let scale = if output.scale > 0.0'f32: output.scale else: 1.0'f32
  %*{
    "id": output.id,
    "name": output.name,
    "connected": true,
    "is_primary": output.isPrimary,
    "refresh_rate": output.refreshRate,
    "physical_width": output.physicalWidth,
    "physical_height": output.physicalHeight,
    "scale": scale,
    "transform": output.transform.outputTransformId(),
    "geometry": {"x": output.x, "y": output.y, "width": output.w, "height": output.h},
  }

proc triadOutputsJson*(snapshot: ShellSnapshot): JsonNode =
  result = newJArray()
  for output in snapshot.outputs:
    result.add(triadOutputJson(output))

proc idleInhibitModeId(mode: WindowRuleIdleInhibitMode): string =
  case mode
  of WindowRuleIdleInhibitMode.IdleInhibitNone: "none"
  of WindowRuleIdleInhibitMode.IdleInhibitFocused: "focused"
  of WindowRuleIdleInhibitMode.IdleInhibitVisible: "visible"

proc triadWindowJson(win: ShellWindow): JsonNode =
  %*{
    "id": win.id,
    "pid":
      if win.pid <= 0:
        newJNull()
      else:
        %win.pid,
    "parent_id":
      if win.parentId == 0:
        newJNull()
      else:
        %win.parentId,
    "title": nullableString(win.title),
    "app_id": nullableString(win.appId),
    "tag_id":
      if win.tagId.isSome:
        %win.tagId.get()
      else:
        newJNull(),
    "workspace_idx":
      if win.workspaceIdx == 0:
        newJNull()
      else:
        %win.workspaceIdx,
    "output": nullableString(win.outputName),
    "position": {
      "column_idx":
        if win.colIdx == 0:
          newJNull()
        else:
          %win.colIdx,
      "window_idx":
        if win.winIdx == 0:
          newJNull()
        else:
          %win.winIdx,
    },
    "is_focused": win.isFocused,
    "is_floating": win.isFloating,
    "is_maximized": win.isMaximized,
    "is_minimized": win.isMinimized,
    "is_sticky": win.isSticky,
    "is_overlay": win.isOverlay,
    "is_unmanaged_global": win.isUnmanagedGlobal,
    "is_fullscreen": win.isFullscreen,
    "fullscreen_output":
      if win.fullscreenOutput == 0:
        newJNull()
      else:
        %win.fullscreenOutput,
    "width_proportion": win.widthProportion,
    "height_proportion": win.heightProportion,
    "actual_size": {"width": win.actualW, "height": win.actualH},
    "floating_geometry": {
      "x": win.floatingGeom.x,
      "y": win.floatingGeom.y,
      "width": win.floatingGeom.w,
      "height": win.floatingGeom.h,
    },
    "keyboard_shortcuts_inhibit": win.keyboardShortcutsInhibit,
    "idle_inhibit": idleInhibitModeId(win.idleInhibitMode),
    "is_terminal": win.isTerminal,
    "allow_swallow": win.allowSwallow,
    "swallowed_by":
      if win.swallowedBy == 0:
        newJNull()
      else:
        %win.swallowedBy,
    "swallowing":
      if win.swallowing == 0:
        newJNull()
      else:
        %win.swallowing,
  }

proc triadWindowsJson*(snapshot: ShellSnapshot): JsonNode =
  result = newJArray()
  for win in snapshot.windows:
    result.add(triadWindowJson(win))

proc triadFocusedWindowJson*(snapshot: ShellSnapshot): JsonNode =
  let focused = snapshot.focusedWindowId()
  for win in snapshot.windows:
    if win.id == focused:
      return triadWindowJson(win)
  newJNull()

proc triadOverviewJson*(snapshot: ShellSnapshot): JsonNode =
  %*{
    "is_open": snapshot.overviewActive,
    "selected_window_id":
      if snapshot.overviewSelectedWindow == 0:
        newJNull()
      else:
        %snapshot.overviewSelectedWindow,
  }

proc triadKeyboardLayoutsJson*(snapshot: ShellSnapshot): JsonNode =
  %*{"names": snapshot.keyboardLayoutNames, "current_idx": snapshot.keyboardLayoutIndex}

proc sortedKeys(table: Table[uint32, uint32]): seq[uint32] =
  for key in table.keys:
    result.add(key)
  result.sort()

proc captureCountArray(table: Table[uint32, uint32]): JsonNode =
  result = newJArray()
  for id in table.sortedKeys():
    result.add(%*{"id": id, "count": table[id]})

proc captureWindowJson(table: Table[uint32, uint32], id: uint32): JsonNode =
  result = %*{"id": id, "count": table[id], "known": false}

proc captureWindowJson(
    table: Table[uint32, uint32], snapshot: ShellSnapshot, id: uint32
): JsonNode =
  result = captureWindowJson(table, id)
  for win in snapshot.windows:
    if win.id != id:
      continue
    result["known"] = %true
    result["app_id"] = %win.appId
    result["title"] = %win.title
    result["identifier"] = %win.identifier
    result["workspace_idx"] = %win.workspaceIdx
    result["tag_id"] =
      if win.tagId.isSome:
        %win.tagId.get()
      else:
        newJNull()
    result["output_name"] = %win.outputName
    result["is_focused"] = %win.isFocused
    result["is_floating"] = %win.isFloating
    result["is_fullscreen"] = %win.isFullscreen
    result["is_minimized"] = %win.isMinimized
    return

proc captureOutputJson(table: Table[uint32, uint32], id: uint32): JsonNode =
  result = %*{"id": id, "count": table[id], "known": false}

proc captureOutputJson(
    table: Table[uint32, uint32], snapshot: ShellSnapshot, id: uint32
): JsonNode =
  result = captureOutputJson(table, id)
  for output in snapshot.outputs:
    if output.id != id:
      continue
    result["known"] = %true
    result["name"] = %output.name
    result["x"] = %output.x
    result["y"] = %output.y
    result["width"] = %output.w
    result["height"] = %output.h
    result["scale"] = %output.scale
    result["transform"] = %output.transform
    result["is_primary"] = %output.isPrimary
    return

proc captureWindowArray(
    table: Table[uint32, uint32], snapshot: ShellSnapshot
): JsonNode =
  result = newJArray()
  for id in table.sortedKeys():
    result.add(captureWindowJson(table, snapshot, id))

proc captureOutputArray(
    table: Table[uint32, uint32], snapshot: ShellSnapshot
): JsonNode =
  result = newJArray()
  for id in table.sortedKeys():
    result.add(captureOutputJson(table, snapshot, id))

proc captureTotal(table: Table[uint32, uint32]): uint32 =
  for count in table.values:
    result += count

proc triadCaptureSessionsJson*(
    windowCaptureSessions, outputCaptureSessions: Table[uint32, uint32]
): JsonNode =
  let windowTotal = windowCaptureSessions.captureTotal()
  let outputTotal = outputCaptureSessions.captureTotal()
  %*{
    "active": windowTotal > 0 or outputTotal > 0,
    "window_total": windowTotal,
    "output_total": outputTotal,
    "windows": windowCaptureSessions.captureCountArray(),
    "outputs": outputCaptureSessions.captureCountArray(),
  }

proc triadCaptureSessionsJson*(
    windowCaptureSessions, outputCaptureSessions: Table[uint32, uint32],
    snapshot: ShellSnapshot,
): JsonNode =
  let windowTotal = windowCaptureSessions.captureTotal()
  let outputTotal = outputCaptureSessions.captureTotal()
  %*{
    "active": windowTotal > 0 or outputTotal > 0,
    "window_total": windowTotal,
    "output_total": outputTotal,
    "windows": windowCaptureSessions.captureWindowArray(snapshot),
    "outputs": outputCaptureSessions.captureOutputArray(snapshot),
  }

proc emptyTriadCaptureSessionsJson*(): JsonNode =
  %*{
    "active": false,
    "window_total": 0,
    "output_total": 0,
    "windows": newJArray(),
    "outputs": newJArray(),
  }

proc captureSessionsOrEmpty*(captureSessions: JsonNode): JsonNode =
  if captureSessions == nil:
    emptyTriadCaptureSessionsJson()
  else:
    captureSessions

proc triadCapabilitiesJson*(captureSessionsSupported = true): JsonNode =
  %*{
    "event_stream": true,
    "state": true,
    "layout_state": true,
    "overview": true,
    "workspace_creation": true,
    "workspace_switching": true,
    "workspace_content_scroll": true,
    "window_focus": true,
    "window_close": true,
    "spawn": true,
    "keyboard_layout": true,
    "output_metadata": true,
    "monitor_power": true,
    "capture_sessions": captureSessionsSupported,
    "workspace_urgency": false,
  }

proc triadStateJson*(
    snapshot: ShellSnapshot,
    captureSessions: JsonNode = nil,
    captureSessionsSupported = true,
): JsonNode =
  let keyboardLayouts = triadKeyboardLayoutsJson(snapshot)

  %*{
    "version": snapshot.version,
    "capabilities": triadCapabilitiesJson(captureSessionsSupported),
    "overview": triadOverviewJson(snapshot),
    "layout": triadLayoutStateJson(snapshot),
    "keyboard_layouts": keyboardLayouts["names"],
    "current_keyboard_layout_idx": keyboardLayouts["current_idx"],
    "outputs": triadOutputsJson(snapshot),
    "windows": triadWindowsJson(snapshot),
    "capture_sessions": captureSessions.captureSessionsOrEmpty(),
  }

proc triadLayoutStateChangedEvent*(snapshot: ShellSnapshot): string =
  $(
    %*{
      "triad": {
        "version": TriadIpcVersion,
        "event": "layout-state-changed",
        "state": triadLayoutStateJson(snapshot),
      }
    }
  )

proc triadStateChangedEvent*(
    snapshot: ShellSnapshot,
    captureSessions: JsonNode = nil,
    captureSessionsSupported = true,
): string =
  $(
    %*{
      "triad": {
        "version": TriadIpcVersion,
        "event": "state-changed",
        "state": triadStateJson(snapshot, captureSessions, captureSessionsSupported),
      }
    }
  )

proc triadWindowChangedEvent*(win: ShellWindow): string =
  $(
    %*{
      "triad": {
        "version": TriadIpcVersion,
        "event": "window-changed",
        "window": triadWindowJson(win),
      }
    }
  )

proc triadCaptureSessionsChangedEvent*(captureSessions: JsonNode): string =
  $(
    %*{
      "triad": {
        "version": TriadIpcVersion,
        "event": "capture-sessions-changed",
        "capture_sessions": captureSessions.captureSessionsOrEmpty(),
      }
    }
  )

proc triadStatePayloadWithCaptureSessions*(
    payload: string, captureSessions: JsonNode, captureSessionsSupported = true
): string =
  try:
    let root = parseJson(payload)
    if root.kind != JObject or not root.hasKey("triad"):
      return payload
    let triad = root["triad"]
    if triad.kind != JObject or not triad.hasKey("state"):
      return payload
    let state = triad["state"]
    if state.kind != JObject:
      return payload
    state["capture_sessions"] = captureSessions.captureSessionsOrEmpty()
    if state.hasKey("capabilities") and state["capabilities"].kind == JObject:
      state["capabilities"]["capture_sessions"] = %captureSessionsSupported
    $root
  except CatchableError:
    payload
